apply plugin: 'idea'
apply plugin: 'java-library'
apply plugin: 'maven-publish'

group 'gg.moonflower'
version '3.0.4-customized.5'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

if (System.getenv('BUILD_NUMBER') != null) {
    version += "." + System.getenv('BUILD_NUMBER')
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.jetbrains:annotations:23.0.0'

    implementation 'org.ow2.asm:asm-tree:9.5'

    testImplementation 'com.google.guava:guava:31.1-jre'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
}

test {
    useJUnitPlatform()
}

java {
    withSourcesJar()
    withJavadocJar()
}

def secretProps = null
if (new File("secret.properties").exists()) {
    secretProps = new File("secret.properties").withReader {
        def prop = new Properties()
        prop.load(it)
        return (new ConfigSlurper().parse(prop))
    }
}

publishing {
    publications {
        myJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        mavenLocal()
        if (secretProps != null) {
            maven {
                url "https://repo.repsy.io/mvn/${secretProps.repsy.user}/${secretProps.repsy.repo_name}"
                credentials {
                    username secretProps.repsy.login
                    password secretProps.repsy.password
                }
            }
        }
    }
}